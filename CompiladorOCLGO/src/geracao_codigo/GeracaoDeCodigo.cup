package geracao_codigo;
import java_cup.runtime.Symbol;
import util.sym;
import java.util.LinkedList;
import util.*;

parser code {:
	
	public void report_error(String message, Object info) {
		Util.report_error(message, info);
	}
	public void syntax_error(Symbol cur_token) {
		Util.syntax_error(cur_token);
	}
	public void debug_message(String mess) {
		Util.debug_message(mess);
	}
	public void debug_shift(Symbol shift_tkn) {
		Util.debug_shift(shift_tkn);
	}
	public void debug_reduce(int prod_num, int nt_num, int rhs_size) {
		Util.debug_reduce(prod_num, nt_num, rhs_size);
	}
:};

terminal PACKAGE, ENDPACKAGE, CONTEXT, BODY, COLLECTION, SELF;
terminal DOT, COMMA, COLON, PATHNAME, LEFT_PAR, LEFT_BRK, LEFT_BRA, RIGHT_PAR, RIGHT_BRK, RIGHT_BRA, BAR;
terminal COLLECTIONOPERATION;
terminal EQ, NE, LE, GE, GT, LT;
terminal AND, OR, NOT, XOR, IMPLIES;
terminal IF, ELSE, THEN, ENDIF;
terminal PLUS, MINUS, TIMES, DIVIDE;
terminal INTEGERTYPE, REALTYPE, BOOLEANTYPE, STRINGTYPE;
terminal Integer INTEGER;
terminal REAL;
terminal String STRING;
terminal IDENTIFIER, BOOLEAN;

non terminal packageDeclaration;
non terminal packageDeclarationAux;
non terminal contextDeclaration;
non terminal bodyDeclaration;
non terminal operation;
non terminal operationParAux;
non terminal oclExpression;
non terminal parameters;
non terminal pathName;
non terminal pathNameAux;
non terminal pathNameOperation;
non terminal collectionTypeIdentifier;
non terminal ifExp;
non terminal collectionType;
non terminal typeName;
non terminal variableDeclaration;
non terminal relationalExpression;
non terminal relationalExpressionAux;
non terminal additiveExpression;
non terminal additiveExpressionAux;
non terminal multiplicativeExpression;
non terminal multiplicativeExpressionAux;
non terminal postfixExpression;
non terminal postfixExpressionAux;
non terminal operationCall;
non terminal propertyCall;
non terminal propertyCallParameters;
non terminal declarator;
non terminal declaratorAux;
non terminal literalCollection;
non terminal expressionParameter;
non terminal expressionParameterAux;
non terminal qualifiers;
non terminal primaryExpression;
non terminal unaryExpression;
non terminal oclExpressionAux;
non terminal unaryOperator;
non terminal multiplyOperator; 
non terminal addOperator;
non terminal relationalOperator;
non terminal logicalOperator;
non terminal literal;
non terminal primaryTypes;
non terminal colonPathName;
 
precedence left COMMA;
precedence left IMPLIES;
precedence left AND, OR, XOR;
precedence left EQ, NE;
precedence left LE, GE, GT, LT; 
precedence left IF, THEN, ELSE, ENDIF;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left NOT;
precedence left LEFT_PAR, RIGHT_PAR;
precedence left COLLECTIONOPERATION, DOT; 

start with packageDeclaration;

packageDeclaration ::=  PACKAGE pathName:pn {:
	XMIParserBasic.getInstancia().setPackage(pn.toString());
	if(!XMIParserBasic.getInstancia().existsPackage()){
		LoggerSemantico.getInstance().addError(StringErro.packageInvalido(pn.toString()));
		throw new ErroFatal();
	}
:}  
packageDeclarationAux ENDPACKAGE

|{:
	XMIParserBasic.getInstancia().setPackage("");
:} 
packageDeclarationAux;

packageDeclarationAux ::= contextDeclaration packageDeclarationAux |;
pathName ::= IDENTIFIER:id pathNameAux:aux {: RESULT = id.toString() + aux.toString(); :};
pathNameAux ::= PATHNAME:pn pathName:path {: RESULT = "::" + path.toString(); :}  
				| {: RESULT = ""; :};
pathNameOperation ::= PATHNAME:pn pathName:path {: RESULT = "::"+ path.toString(); :};

contextDeclaration ::= CONTEXT operation:op {:
	Elemento val = (Elemento) op;
	XMIParserBasic.getInstancia().setContext(val.getAtributo("string"));
	if(!XMIParserBasic.getInstancia().existsContext()){
		LoggerSemantico.getInstance().addError(StringErro.contextoInvalido(val.toString()));
		throw new ErroFatal();
	}
	Gerador.getInstance().setFuncao(val.getAtributo("operacao"));
:} 

bodyDeclaration:b {: 
	Elemento val = (Elemento) op;
	val.setAtributo("tipo", XMIParserBasic.getInstancia().getOperationType(val.getAtributo("operacao")));	
	Elemento body = (Elemento) b;
	
	if(!Elemento.tiposEquivalentes(val.getAtributo("tipo"), body.getAtributo("tipo"))){
		LoggerSemantico.getInstance().addError(StringErro.tipoConsultaInvalida(body.toString(), val.getAtributo("tipo"), body.getAtributo("tipo")));
	}
	
	val.setAtributo("tipo", Elemento.max(val.getAtributo("tipo"), body.getAtributo("tipo")));
	RESULT = val;
	XMIParserBasic.getInstancia().deleteAllVariaveis();
:};

bodyDeclaration ::= BODY COLON oclExpression:exp {: 
	RESULT = exp;
:};

operation ::= IDENTIFIER:id pathNameOperation:pno LEFT_PAR operationParAux:ope RIGHT_PAR COLON typeName:type{:
	
	LinkedList<Parametro> opeParametros = (LinkedList<Parametro>) ope;
	Elemento typeElem = (Elemento) type;
	
	Elemento val = new Elemento(id.toString() + pno.toString(), idleft, idright);
	String[] splited = pno.toString().split("::");
	val.setAtributo("operacao", splited[splited.length - 1]);
	
	Gerador.getInstance().addFuncao(splited[splited.length - 2], splited[splited.length - 1], opeParametros, typeElem.getAtributo("code"));
	val.setAtributo("string", id.toString() + pno.toString() + "(" + Elemento.parametrosToString(opeParametros) + ")" + ":" + typeElem.toString());
	
	RESULT =  val;
:};
			  
operationParAux ::= parameters:p {: RESULT = p; :}  
|{: RESULT = new LinkedList<Parametro>(); :};
					
parameters ::= variableDeclaration:v {:
	Elemento vElem = (Elemento) v;
	String str[] = vElem.getAtributo("string").split(":");
	
	LinkedList<Parametro> pa = new LinkedList<Parametro>();
	Parametro p = new Parametro(str[0], str[1]);
	
	pa.add(p);
	
	RESULT = pa; 
:}

| variableDeclaration:v COMMA parameters:p {:
	LinkedList<Parametro> pa = (LinkedList<Parametro>) p;
	
	Elemento vElem = (Elemento) v;
	String str[] = vElem.getAtributo("string").split(":");
	
	Parametro par = new Parametro(str[0], str[1]);
	
	pa.add(par);
	 
	RESULT = pa; 
:};

collectionType ::= collectionTypeIdentifier:c LEFT_PAR typeName:t RIGHT_PAR {:
	Elemento r = new Elemento(c.toString() + "(" + t.toString() + ")");
	r.setAtributo("code", c.toString() + "(" + t.toString() + ")");
	RESULT = r;
:};

collectionTypeIdentifier ::= COLLECTION:c {: RESULT = c.toString(); :};
			 
typeName ::= primaryTypes:p {: RESULT = p; :} 
			| pathName:pna {: RESULT = pna.toString(); :}
			| collectionType:c {: RESULT = c; :};

primaryTypes ::= 
STRINGTYPE:s {:
	Elemento r = new Elemento("String");
	r.setAtributo("code", "string");
	RESULT = r; 
:} 
| REALTYPE:r {:
	Elemento re = new Elemento("Real");
	re.setAtributo("code", "float");
	RESULT = r; 
:}
| BOOLEANTYPE:b {: 
	Elemento r = new Elemento("Boolean");
	r.setAtributo("code", "bool");
	RESULT = r; 
:}
| INTEGERTYPE:i {: 
	Elemento r = new Elemento("Integer");
	r.setAtributo("code", "int");
	RESULT = r; 
:};
			
literal ::= REAL:v {:
	Elemento elem = new Elemento(v.toString(), vleft, vright);
	elem.setAtributo("tipo", "Real");
	elem.setAtributo("code", v.toString());
	RESULT = elem;
:}
| INTEGER:v {:
	Elemento elem = new Elemento(v.toString(), vleft, vright);
	elem.setAtributo("tipo", "Integer");
	elem.setAtributo("code", v.toString());
	RESULT = elem;
:} 
| BOOLEAN:v {:
	Elemento elem = new Elemento(v.toString(), vleft, vright);
	elem.setAtributo("tipo", "Boolean");
	elem.setAtributo("code", v.toString());
	RESULT = elem;
:} 
| STRING:v {:
	Elemento elem = new Elemento(v.toString(), vleft, vright);
	elem.setAtributo("tipo", "String");
	elem.setAtributo("code", v.toString());
	RESULT = elem;
:};
			
variableDeclaration ::= IDENTIFIER:id COLON typeName:type {:
	XMIParserBasic.getInstancia().addVariavel(id.toString(),type.toString());
	Elemento r = new Elemento(id.toString(), idleft, idright);
	r.setAtributo("code", id.toString() + " " + type.toString());
	r.setAtributo("string", id.toString() + ":" + type.toString());
	RESULT = r;
:};

oclExpression ::= relationalExpression:r oclExpressionAux:aux {:
	RESULT = r;
	if(aux != null){
		Elemento rel = (Elemento) r;
		if(rel.getAtributo("tipo") != "Boolean"){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(rel.toString(), "Boolean"));
		}
		
	}
	
:};
oclExpressionAux ::= logicalOperator relationalExpression:r oclExpressionAux {:
	Elemento rel = (Elemento) r;  
	if(rel.getAtributo("tipo") != "Boolean"){
		LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(rel.toString(), "Boolean"));
	}
	RESULT = rel; 
:} 
|;

relationalExpression ::= additiveExpression:add relationalExpressionAux:aux {:
	Elemento val = (Elemento) add;
	if(aux != null){
		Elemento auxElem = (Elemento) aux; 
		if((auxElem.getAtributo("operacao") != "=") && (auxElem.getAtributo("operacao") != "<>")){
			if((!val.getAtributo("tipo").equalsIgnoreCase("Real")) && (!val.getAtributo("tipo").equalsIgnoreCase("Integer"))){
				LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
			}
		}else{
			if(!Elemento.tiposEquivalentes(val.getAtributo("tipo"), auxElem.getAtributo("tipo"))){
				LoggerSemantico.getInstance().addError(StringErro.tipoDiferentes(val.toString(), auxElem.toString(), val.getAtributo("tipo"), auxElem.getAtributo("tipo")));
			}
		}
		val.setAtributo("tipo", "Boolean");
		val.setAtributo("code", TemplateCode.RELATIONALEXPRESSION(val.getAtributo("code"), auxElem.getAtributo("operacaoCode"), auxElem.getAtributo("code")));
	}
	RESULT = val;
:}; 
relationalExpressionAux ::= relationalOperator:op additiveExpression:add relationalExpressionAux:aux {:
	Elemento val = (Elemento) add;
	Elemento oper = (Elemento) op;
	val.setAtributo("code", val.getAtributo("code"));
	val.setAtributo("operacao", oper.toString());
	val.setAtributo("operacaoCode", oper.getAtributo("code"));
	if((val.getAtributo("operacao") != "=") && (val.getAtributo("operacao") != "<>")){
		if((val.getAtributo("tipo") != "Real") && (val.getAtributo("tipo") != "Integer") ){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
		}
	}
	if(aux != null){
		Elemento auxElem = (Elemento) aux;
		if((auxElem.getAtributo("operacao") == "=") || (auxElem.getAtributo("operacao") == "<>")){
			if(!Elemento.tiposEquivalentes(val.getAtributo("tipo"), auxElem.getAtributo("tipo"))){
				LoggerSemantico.getInstance().addError(StringErro.tipoDiferentes(val.toString(), auxElem.toString(), val.getAtributo("tipo"), auxElem.getAtributo("tipo")));
			}
		}
		auxElem.coercao(val.getAtributo("tipo"));
		val.setAtributo("tipo", "Boolean");
		val.setAtributo("code", TemplateCode.RELATIONALEXPRESSION(val.getAtributo("code"), auxElem.getAtributo("operacaoCode"),auxElem.getAtributo("code")));
	}
	RESULT = val;
:} 
|;

additiveExpression ::= multiplicativeExpression:mult additiveExpressionAux:aux {:
	Elemento val = (Elemento) mult;
	if(aux != null){ 
		if((val.getAtributo("tipo") != "Real") && (val.getAtributo("tipo") != "Integer") ){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
		}
	}
	RESULT = mult;
:};

additiveExpressionAux ::= addOperator multiplicativeExpression:mult additiveExpressionAux {:
	Elemento val = (Elemento) mult;
	if((val.getAtributo("tipo") != "Real") && (val.getAtributo("tipo") != "Integer") ){
		LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
	}
	RESULT = mult;
:} 
|;

multiplicativeExpression ::= unaryExpression:un multiplicativeExpressionAux:aux {:
	Elemento val = (Elemento) un;
	if(aux != null){ 
		if((val.getAtributo("tipo") != "Real") && (val.getAtributo("tipo") != "Integer") ){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
		}
	}
	RESULT = un;
:};
multiplicativeExpressionAux ::= multiplyOperator unaryExpression:un multiplicativeExpressionAux{:
	Elemento val = (Elemento) un;
	if((val.getAtributo("tipo") != "Real") && (val.getAtributo("tipo") != "Integer") ){
		LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
	}
	RESULT = un;
:} 
|;

unaryExpression ::= unaryOperator:op postfixExpression:post {:
	Elemento val = (Elemento) post;
	if(op.toString() == "NOT"){
		if(val.getAtributo("tipo") != "Boolean"){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Boolean"));
		}
	}else if(op.toString() == "-"){
		if((val.getAtributo("tipo") != "Real") && (val.getAtributo("tipo") != "Integer") ){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Integer ou Real"));
		}
	}
	val.valor = op.toString() + " " + val.valor;
	RESULT = post;
:}
| postfixExpression:post {: RESULT = post; :};

postfixExpression ::= primaryExpression:pri {:
	Elemento val = (Elemento) pri;
	String tipo = val.getAtributo("tipo");
	if(Elemento.ehCollection(tipo)){
		tipo = Elemento.tipoCollection(tipo);
	}
	XMIParserBasic.getInstancia().setTempContext(tipo);
:}
postfixExpressionAux:aux {:
    Elemento source = (Elemento) pri;
    if(aux != null){
    	Elemento post = (Elemento) aux;
    	Elemento res = new Elemento(source.valor, prileft, priright);
    	res.setAtributo("tipo", post.getAtributo("tipo"));
		if(post.getAtributo("collection") == "true"){
			if(!Elemento.ehCollection(source.getAtributo("tipo"))){
				LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(source.toString(), "Collection"));
				throw new ErroFatal();
			}
		}
		if(post.valor.equalsIgnoreCase("any") || post.valor.equalsIgnoreCase("forAll") || post.valor.equalsIgnoreCase("exists")){
			res.setAtributo("especial", "true");
		} else if(post.getAtributo("operacao") == "."){
			if(post.getAtributo("especial") != null){
				res.setAtributo("especial", "true");
			}else if(Elemento.ehCollection(source.getAtributo("tipo")) && !Elemento.ehCollection(res.getAtributo("tipo"))){
				res.setAtributo("tipo", "Collection(" + res.getAtributo("tipo") + ")");
			}
		}
		RESULT = res;
	}else{
		RESULT = pri;
	}
	if(Elemento.ehCollection(source.getAtributo("tipo"))){
		XMIParserBasic.getInstancia().desativarNavegando(Elemento.tipoCollection(source.getAtributo("tipo")));
	}else{
		XMIParserBasic.getInstancia().desativarNavegando(source.getAtributo("tipo"));
	}
:};

postfixExpressionAux ::= operationCall:op {:
	XMIParserBasic.getInstancia().ativarNavegando();
:}
propertyCall:p {:
	Elemento val = (Elemento) p;
	String tipo = val.getAtributo("tipo");
	if(Elemento.ehCollection(tipo)){
		tipo = Elemento.tipoCollection(tipo);
	}
	XMIParserBasic.getInstancia().setTempContext(tipo);
:}
postfixExpressionAux:aux {:
	Elemento pro = (Elemento) p; 
	if(op.toString() == "->"){
		if(pro.getAtributo("collection") != "true"){
			LoggerSemantico.getInstance().addError(StringErro.funcaoOperacaoInvalida(pro.toString()));
			throw new ErroFatal();
		}
	}
	if(op.toString() == "."){
		if(Elemento.ehOperacaoColecao(pro.valor)){
			LoggerSemantico.getInstance().addError(StringErro.atributoOperacaoInvalida(pro.toString()));
			throw new ErroFatal();
		}
	}
	if(aux != null){
		Elemento post = (Elemento) aux;
    	Elemento res = new Elemento(pro.valor, pleft, pright);
    	res.setAtributo("tipo", post.getAtributo("tipo"));
		if(post.getAtributo("collection") == "true"){
			if(!Elemento.ehCollection(pro.getAtributo("tipo"))){
				LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(pro.toString(), "Collection"));
				throw new ErroFatal();
			}
		}
		if(post.valor.equalsIgnoreCase("any") || post.valor.equalsIgnoreCase("forAll") || post.valor.equalsIgnoreCase("exists") || post.getAtributo("especial") != null){
			res.setAtributo("especial", "true");
		} else if(post.getAtributo("operacao") == "."){
			if(post.getAtributo("especial") != null){
				res.setAtributo("especial", "true");
			}else if(Elemento.ehCollection(pro.getAtributo("tipo")) && !Elemento.ehCollection(res.getAtributo("tipo"))){
				res.setAtributo("tipo", "Collection(" + res.getAtributo("tipo") + ")");
			}
		}
		res.setAtributo("operacao",op.toString());
		RESULT = res;
	}else{
		if(pro.valor.equalsIgnoreCase("any") || pro.valor.equalsIgnoreCase("forAll") || pro.valor.equalsIgnoreCase("exists")){
			pro.setAtributo("especial", "true");
		}
		pro.setAtributo("operacao",op.toString());
		RESULT = pro;
	}
	if(Elemento.ehCollection(pro.getAtributo("tipo"))){
		XMIParserBasic.getInstancia().desativarNavegando(Elemento.tipoCollection(pro.getAtributo("tipo")));
	}else{
		XMIParserBasic.getInstancia().desativarNavegando(pro.getAtributo("tipo"));
	}
:} 
|;

operationCall ::= COLLECTIONOPERATION {: RESULT = "->"; :} 
				  | DOT {: RESULT = "."; :};
				  
primaryExpression ::= literalCollection:l {: RESULT = l; :}
					| literal:li {: RESULT = li; :}
					| propertyCall:p {: RESULT = p; :}
					| LEFT_PAR oclExpression:exp RIGHT_PAR {: RESULT = exp; :}
| SELF:id {:
	String tipo = XMIParserBasic.getInstancia().getAttributeType(id.toString());
	Elemento val = new Elemento(id.toString(), idleft, idright);
	if(tipo == null){
		LoggerSemantico.getInstance().addError(StringErro.naoExiste("Identificador", val.toString()));
		throw new ErroFatal();
	}
	
	val.setAtributo("tipo", tipo);
	RESULT = val;
:}					
					| ifExp:exp {: RESULT = exp; :};

literalCollection ::= collectionTypeIdentifier:col LEFT_BRA expressionParameter:exp RIGHT_BRA {:
	Parametros p = (Parametros) exp;
	String tipo = p.tipoParametrosIguais();
	if(tipo == null){
		LoggerSemantico.getInstance().addError(StringErro.invalidoCollectionLiteral());
		throw new ErroFatal();
	}else{
		Elemento e = new Elemento(col.toString(), colleft, colright);
		e.setAtributo("tipo", e.valor + "(" + tipo + ")");
		RESULT = e;
	}
:};
 
ifExp ::= IF:ifToken oclExpression:exp THEN oclExpression:exp1 ELSE oclExpression:exp2 ENDIF {:
	Elemento val = (Elemento) exp;
	Elemento val1 = (Elemento) exp1;
	Elemento val2 = (Elemento) exp2;
	
	Elemento res = new Elemento("Comando IF", ifTokenleft, ifTokenright);
	res.setAtributo("tipo", val1.getAtributo("tipo"));
	
	if(val.getAtributo("tipo") != "Boolean"){
		LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), "Boolean"));
	}
	if(!Elemento.tiposEquivalentes(val1.getAtributo("tipo"), val2.getAtributo("tipo"))){
		LoggerSemantico.getInstance().addError(StringErro.tipoDiferentes(val1.toString(), val2.toString(), val1.getAtributo("tipo"), val2.getAtributo("tipo")));
	}else{
		res.setAtributo("tipo", Elemento.max(val1.getAtributo("tipo"), val2.getAtributo("tipo")));
	}
	
	Gerador.getInstance().addIF(val.getAtributo("code"), val1.getAtributo("code"), val2.getAtributo("code"), res.getAtributo("tipo"));  
	
	RESULT = res;
:}
;

propertyCall ::= pathName:p qualifiers propertyCallParameters:par {:
	String tipo = XMIParserBasic.getInstancia().getOperationType(p.toString());
	Elemento val = new Elemento(p.toString(), pleft, pright);
	
	if(tipo == null){
		tipo = XMIParserBasic.getInstancia().getAttributeType(p.toString());
		if(tipo == null){
			LoggerSemantico.getInstance().addError(StringErro.naoExiste("Identificador", val.toString()));
			throw new ErroFatal();
		}
		if(par != null){
			LoggerSemantico.getInstance().addError(StringErro.naoExiste("Funcao", val.toString()));
			throw new ErroFatal();
		}
	}else{
	    
		LinkedList<String> parametros = (LinkedList<String>) XMIParserBasic.getInstancia().getParametersType(p.toString());
			
		if(!((Parametros) par).parametrosValidos(parametros)){
			LoggerSemantico.getInstance().addError(StringErro.parametrosInvalidos(val.toString(), parametros, ((Parametros) par).lista));
			throw new ErroFatal();
		}
	}
	val.setAtributo("collection", "false");
	if(Elemento.ehOperacaoColecao(val.valor)){
		val.setAtributo("collection", "true");
	}
	val.setAtributo("tipo", tipo);
	RESULT = val;
	
:};

qualifiers ::= LEFT_BRK expressionParameter RIGHT_BRK|;

propertyCallParameters ::= LEFT_PAR expressionParameter:exp RIGHT_PAR {: RESULT = exp; :}
 
|LEFT_PAR declarator:d RIGHT_PAR {:
	Parametros dec = (Parametros) d;
	Parametros ret = new Parametros();
	ret.push(dec.lista.get(0));  
	RESULT = ret; 
	XMIParserBasic.getInstancia().deleteVariavel(dec.lista);
:}
						   |;
						   
declarator ::= IDENTIFIER:id declaratorAux:aux colonPathName:path  BAR {:
	Parametros p = (Parametros) aux;
	Elemento val = new Elemento(id.toString(), idleft, idright);
	if(XMIParserBasic.getInstancia().containsVariavel(val.valor) || p.lista.contains(val.valor)){
		LoggerSemantico.getInstance().addError(StringErro.jaExiste("Identificador", val.toString()));
		throw new ErroFatal();
	}
	String tipo = XMIParserBasic.getInstancia().context.lastElement();
	p.push(val.valor);
	if(path != null){
		if(!Elemento.tiposEquivalentes(XMIParserBasic.getInstancia().context.lastElement(), path.toString())){
			LoggerSemantico.getInstance().addError(StringErro.tipoInvalido(val.toString(), XMIParserBasic.getInstancia().context.lastElement()));
			throw new ErroFatal();
		}
		tipo = Elemento.max(XMIParserBasic.getInstancia().context.lastElement(), path.toString());
	}
	XMIParserBasic.getInstancia().addVariavel(p.lista,tipo);
:}
oclExpression:exp {:
	Elemento val = (Elemento) exp;
	Parametros p = (Parametros) aux;
	p.push(id.toString());
	p.push(val.getAtributo("tipo"));
	RESULT = p;
:};

declaratorAux ::= COMMA IDENTIFIER:id declaratorAux:aux {:
	Parametros p = (Parametros) aux;
	Elemento val = new Elemento(id.toString(), idleft, idright);
	if(XMIParserBasic.getInstancia().containsVariavel(val.valor) || p.lista.contains(val.valor)){
		LoggerSemantico.getInstance().addError(StringErro.jaExiste("Identificador", val.toString()));
		throw new ErroFatal();
	}
	p.push(val.valor);
	RESULT = p;
:}

| {: RESULT = new Parametros(); :};

colonPathName ::= COLON pathName:pa {: RESULT = pa.toString(); :} |;

expressionParameter ::= oclExpression:exp expressionParameterAux:aux {:
	Elemento val = (Elemento) exp;
	Parametros p = (Parametros) aux;
	p.push(val.getAtributo("tipo"));
	RESULT = p;
:}
|IDENTIFIER:id expressionParameterAux:aux {:
	String tipo = XMIParserBasic.getInstancia().getAttributeType(id.toString());
	Elemento val = new Elemento(id.toString(), idleft, idright);
	if(tipo == null){
		LoggerSemantico.getInstance().addError(StringErro.naoExiste("Identificador", val.toString()));
		throw new ErroFatal();
	}
	
	val.setAtributo("tipo", tipo);
	Parametros p = (Parametros) aux;
	p.push(tipo);
	RESULT = p;
:}
| {: RESULT = new Parametros(); :};

expressionParameterAux ::= COMMA oclExpression:exp expressionParameterAux:aux {:
	Elemento val = (Elemento) exp;
	Parametros p = (Parametros) aux;
	p.push(val.getAtributo("tipo"));
	RESULT = p;
:}
|COMMA IDENTIFIER:id expressionParameterAux:aux {:
	String tipo = XMIParserBasic.getInstancia().getAttributeType(id.toString());
	Elemento val = new Elemento(id.toString(), idleft, idright);
	if(tipo == null){
		LoggerSemantico.getInstance().addError(StringErro.naoExiste("Identificador", id.toString()));
		throw new ErroFatal();
	}
	
	val.setAtributo("tipo", tipo);
	Parametros p = (Parametros) aux;
	p.push(tipo);
	RESULT = p;
:} 
| {: RESULT = new Parametros(); :};

logicalOperator ::= AND | OR | XOR | IMPLIES;

relationalOperator ::= EQ {:
	Elemento elem = new Elemento("=");
	elem.setAtributo("code", "=="); 
	RESULT = elem; 
:} 
|NE {: 
	Elemento elem = new Elemento("<>");
	elem.setAtributo("code", "!="); 
	RESULT = elem; 
:}
|LE {: 
	Elemento elem = new Elemento("<=");
	elem.setAtributo("code", "<="); 
	RESULT = elem; 
:}
|GE {: 
	Elemento elem = new Elemento(">=");
	elem.setAtributo("code", ">="); 
	RESULT = elem; 
:}
|GT {: 
	Elemento elem = new Elemento(">");
	elem.setAtributo("code", ">"); 
	RESULT = elem;
:}
|LT {: 
	Elemento elem = new Elemento("<");
	elem.setAtributo("code", "<"); 
	RESULT = elem; 
:};

addOperator ::= PLUS {:
	Elemento elem = new Elemento("+");
	elem.setAtributo("code", "+"); 
	RESULT = elem;
:} 
|MINUS{:
	Elemento elem = new Elemento("-");
	elem.setAtributo("code", "-"); 
	RESULT = elem;
:};

multiplyOperator ::= TIMES{:
	Elemento elem = new Elemento("*");
	elem.setAtributo("code", "*"); 
	RESULT = elem;
:} 
|DIVIDE{:
	Elemento elem = new Elemento("/");
	elem.setAtributo("code", "/"); 
	RESULT = elem;
:};

unaryOperator ::= MINUS {:
	Elemento elem = new Elemento("-");
	elem.setAtributo("code", "-"); 
	RESULT = elem; 
:}
|NOT {: 
	Elemento elem = new Elemento("NOT");
	elem.setAtributo("code", "!"); 
	RESULT = elem; 
:};